CS224
Lab No. 4
Section No. 1
Full Name: Omer Oktay Gultekin
Bilkent ID: 21901413
Date: 04.04.2022

# fpga_top module

`timescale 1ns / 1ps

module fpga_top(input logic clkBtn, rstBtn, clk,
                output logic memwrite, dp,
                output logic [3:0] an,
                output logic [6:0] seg);
    // new clk and reset controlled by buttons
    logic pushedClk, pushedRst;
    
    // used variables
    logic[31:0] pc, instr, dataadr, writedata, readdata;
    
    // pulse controllers initialization
    pulse_controller pulseClk(clk, clkBtn, 1'b0, pushedClk);
    pulse_controller pulseRst(clk, rstBtn, 1'b0, pushedRst);
    
    // Seven segment display initialization
    display_controller sevenSeg(clk, writedata[7:4], writedata[3:0], dataadr[7:4], dataadr[3:0], seg, dp, an);
    // Mips initialization
    top mips(pushedClk, pushedRst, writedata, dataadr, pc, instr, readdata, memwrite);    
    
endmodule

# top module

`timescale 1ns / 1ps

// Written by David_Harris@hmc.edu

// Top level system including MIPS and memories


module top  (input   logic 	 clk, reset,            
	     output  logic[31:0] writedata, dataadr, 
	     output  logic[31:0] pc, instr, readdata,           
	     output  logic       memwrite);    

   // instantiate processor and memories  
   mips mips (clk, reset, pc, instr, memwrite, dataadr, writedata, readdata);  
   imem imem (pc[7:2], instr);  
   dmem dmem (clk, memwrite, dataadr, writedata, readdata);

endmodule



// External data memory used by MIPS single-cycle processor

module dmem (input  logic        clk, we,
             input  logic[31:0]  a, wd,
             output logic[31:0]  rd);

   logic  [31:0] RAM[63:0];
  
   assign rd = RAM[a[31:2]];    // word-aligned  read (for lw)

   always_ff @(posedge clk)
     if (we)
       RAM[a[31:2]] <= wd;      // word-aligned write (for sw)

endmodule



// External instruction memory used by MIPS single-cycle
// processor. It models instruction memory as a stored-program 
// ROM, with address as input, and instruction as output


module imem ( input logic [5:0] addr, output logic [31:0] instr);

// imem is modeled as a lookup table, a stored-program byte-addressable ROM
	always_comb
	   case ({addr,2'b00})		   	// word-aligned fetch
//		address		instruction
//		-------		-----------
		8'h00: instr = 32'h20020005;  	// disassemble, by hand 
		8'h04: instr = 32'h2003000c;  	// or with a program,
		8'h08: instr = 32'h2067fff7;  	// to find out what
		8'h0c: instr = 32'h00e22025;  	// this program does!
		8'h10: instr = 32'h00642824;
		8'h14: instr = 32'h00a42820;
		8'h18: instr = 32'h10a7000a;
		8'h1c: instr = 32'h0064202a;
		8'h20: instr = 32'h10800001;
		8'h24: instr = 32'h20050000;
		8'h28: instr = 32'h00e2202a;
		8'h2c: instr = 32'h00853820;
		8'h30: instr = 32'h00e23822;
		8'h34: instr = 32'hac670044;
		8'h38: instr = 32'h8c020050;
		8'h3c: instr = 32'h08000011;
		8'h40: instr = 32'h20020001;
		8'h44: instr = 32'hac020054;
		8'h48: instr = 32'h20040005;    // addi $a0, $zero, 5
		8'h4c: instr = 32'h2005000c;    // addi $a1, $zero, 12
		8'h50: instr = 32'h20060002;    // addi $a2, $zero, 2
		8'h54: instr = 32'hf8a6203f;    // srcacc $a0, $a1, $a2
		8'h58: instr = 32'h20040050;    // addi $a0, $zero, 0x50
		8'h5c: instr = 32'h20050002;    // addi $a1, $zero, 2
		8'h60: instr = 32'h20060008;    // addi $a2, $zero, 8
		8'h64: instr = 32'h10a60003;    // beq  $a1, $a2,   3
		8'h68: instr = 32'hfc850000;    // sw+  $a1, 0($a0)
		8'h6c: instr = 32'h20a50002;    // addi $a1, $a1, 2
		8'h70: instr = 32'h08000019;    // j 64, to beq instruction 
		8'h74: instr = 32'h00043820;    // add $a3, $zero, $a0
		8'h78: instr = 32'h8c020050;    // lw $v0, 50($zero)
		8'h7c: instr = 32'h00023820;    // add $a3, $zero, $v0
		8'h80: instr = 32'h08000020;	// j 80, so it will loop here
	    default:  instr = {32{1'bx}};	// unknown address
	   endcase
endmodule


// single-cycle MIPS processor, with controller and datapath

module mips (input  logic        clk, reset,
             output logic[31:0]  pc,
             input  logic[31:0]  instr,
             output logic        memwrite,
             output logic[31:0]  aluout, writedata,
             input  logic[31:0]  readdata);

  logic        memtoreg, pcsrc, zero, alusrc, regwrite, jump, WDSrc, RegWriteSrc;
  logic [1:0]  regdst;
  logic [2:0]  alucontrol;

  controller c (instr[31:26], instr[5:0], zero, memtoreg, memwrite, pcsrc,
                        alusrc, regdst[1:0], jump, regwrite, alucontrol, WDSrc, RegWriteSrc);

  datapath dp (clk, reset, memtoreg, pcsrc, alusrc, regdst[1:0], regwrite, jump,
                          alucontrol, RegWriteSrc, WDSrc, zero, pc, instr, aluout, writedata, readdata);

endmodule
module controller(input  logic[5:0] op, funct,
                  input  logic      zero,
                  output logic      memtoreg, memwrite,
                  output logic      pcsrc, alusrc,
                  output logic[1:0] regdst,
                  output logic      jump, regwrite,
                  output logic[2:0] alucontrol,
                  output logic      WDSrc,
                  output logic      RegWriteSrc);

   logic [1:0] aluop;
   logic       branch;
   
   maindec md (op, memtoreg, memwrite, branch, alusrc, regdst, regwrite, 
		 jump, WDSrc, RegWriteSrc, aluop);

   aludec  ad (funct, aluop, alucontrol);

   assign pcsrc = branch & zero;
endmodule

module maindec (input logic[5:0]    op, 
	              output logic      memtoreg, memwrite, branch,
	              output logic      alusrc, 
	              output logic[1:0] regdst,
	              output logic      regwrite, jump,
	              output logic      WDSrc,
	              output logic      RegWriteSrc,
	              output logic[1:0] aluop);
   logic [11:0] controls;

   assign {regwrite, regdst, alusrc, branch, memwrite,
                memtoreg,  aluop, jump, WDSrc, RegWriteSrc} = controls;

  always_comb
    case(op)
      6'b000000: controls <= 12'b110000010000; // R-type
      6'b100011: controls <= 12'b100100100000; // LW
      6'b101011: controls <= 12'b000101000000; // SW
      6'b000100: controls <= 12'b000010001000; // BEQ
      6'b001000: controls <= 12'b100100000000; // ADDI
      6'b000010: controls <= 12'b000000000100; // J
      6'b111110: controls <= 12'b110000010010; // sracc
      6'b111111: controls <= 12'b101101000011; // sw+
      default:   controls <= 12'bxxxxxxxxxxxx; // illegal op
    endcase
endmodule

module aludec (input    logic[5:0] funct,
               input    logic[1:0] aluop,
               output   logic[2:0] alucontrol);
  always_comb
    case(aluop)
      2'b00: alucontrol  = 3'b010;  // add  (for lw/sw/addi)
      2'b01: alucontrol  = 3'b110;  // sub   (for beq)
      default: case(funct)          // R-TYPE instructions
          6'b100000: alucontrol  = 3'b010; // ADD
          6'b100010: alucontrol  = 3'b110; // SUB
          6'b100100: alucontrol  = 3'b000; // AND
          6'b100101: alucontrol  = 3'b001; // OR
          6'b101010: alucontrol  = 3'b111; // SLT
          6'b111111: alucontrol  = 3'b011; // SRA
          default:   alucontrol  = 3'bxxx; // ???
        endcase
    endcase
endmodule

module datapath (input  logic       clk, reset, memtoreg, pcsrc, alusrc,
                 input  logic[1:0]  regdst,
                 input  logic       regwrite, jump, 
		         input  logic[2:0]  alucontrol,
		         input  logic       RegWriteSrc,
		         input  logic       WDSrc, 
                 output logic       zero,
		         output logic[31:0] pc, 
	             input  logic[31:0] instr,
                 output logic[31:0] aluout, writedata, 
	             input  logic[31:0] readdata);

  logic [4:0]  writereg;
  logic [31:0] pcnext, pcnextbr, pcplus4, pcbranch;
  logic [31:0] signimm, signimmsh, srca, srcb, srcc, result, addSrca, addSrcb, addRes, wdInp;
 
  // next PC logic
  flopr #(32) pcreg(clk, reset, pcnext, pc);
  adder       pcadd1(pc, 32'b100, pcplus4);
  sl2         immsh(signimm, signimmsh);
  adder       pcadd2(pcplus4, signimmsh, pcbranch);
  mux2 #(32)  pcbrmux(pcplus4, pcbranch, pcsrc,
                      pcnextbr);
  mux2 #(32)  pcmux(pcnextbr, {pcplus4[31:28], 
                    instr[25:0], 2'b00}, jump, pcnext);

// register file logic
   regfile     rf (clk, regwrite, instr[25:21], instr[20:16], writereg,
                   wdInp, srca, writedata, srcc);

   mux4 #(5)    wrmux (instr[20:16], instr[25:21], instr[15:11], 5'b00000, regdst[1:0], writereg);
   mux2 #(32)  resmux (aluout, readdata, memtoreg, result);
   signext         se (instr[15:0], signimm);
   mux2 #(32)  addSrcamux  (srcc, srca, RegWriteSrc, addSrca);
   mux2 #(32)  addSrcbmux  (result, 32'h00000004, RegWriteSrc, addSrcb);
   adder       addSrc(addSrca, addSrcb, addRes);
   mux2 #(32)  wdmux  (result, addRes, WDSrc, wdInp);
  // ALU logic
   mux2 #(32)  srcbmux (writedata, signimm, alusrc, srcb);
   alu         alu (srca, srcb, alucontrol, aluout, zero);

endmodule


module regfile (input    logic clk, we3, 
                input    logic[4:0]  ra1, ra2, wa3, 
                input    logic[31:0] wd3, 
                output   logic[31:0] rd1, rd2, rd3);

  logic [31:0] rf [31:0];

  // three ported register file: read two ports combinationally
  // write third port on rising edge of clock. Register0 hardwired to 0.

  always_ff@(posedge clk)
     if (we3) 
         rf [wa3] <= wd3;	

  assign rd1 = (ra1 != 0) ? rf [ra1] : 0;
  assign rd2 = (ra2 != 0) ? rf [ra2] : 0;
  assign rd3 = (wa3 != 0) ? rf [wa3] : 0;

endmodule


module alu(input  logic [31:0] a, b, 
           input  logic [2:0]  alucont, 
           output logic [31:0] result,
           output logic zero);
    
    always_comb
        case(alucont)
            3'b010: result = a + b;
            3'b011: result = a >> b;
            3'b110: result = a - b;
            3'b000: result = a & b;
            3'b001: result = a | b;
            3'b111: result = (a < b) ? 1 : 0;
            default: result = {32{1'bx}};
        endcase
    
    assign zero = (result == 0) ? 1'b1 : 1'b0;
endmodule


module adder (input  logic[31:0] a, b,
              output logic[31:0] y);
     
     assign y = a + b;
endmodule

module sl2 (input  logic[31:0] a,
            output logic[31:0] y);
     
     assign y = {a[29:0], 2'b00}; // shifts left by 2
endmodule

module signext (input  logic[15:0] a,
                output logic[31:0] y);
              
  assign y = {{16{a[15]}}, a};    // sign-extends 16-bit a
endmodule

// parameterized register
module flopr #(parameter WIDTH = 8)
              (input logic clk, reset, 
	       input logic[WIDTH-1:0] d, 
               output logic[WIDTH-1:0] q);

  always_ff@(posedge clk, posedge reset)
    if (reset) q <= 0; 
    else       q <= d;
endmodule


// paramaterized 2-to-1 MUX
module mux2 #(parameter WIDTH = 8)
             (input  logic[WIDTH-1:0] d0, d1,  
              input  logic s, 
              output logic[WIDTH-1:0] y);
  
   assign y = s ? d1 : d0; 
endmodule

// paramaterized 4-to-1 MUX
module mux4 #(parameter WIDTH = 8)
             (input  logic[WIDTH-1:0] d0, d1, d2, d3,  
              input  logic[1:0] s, 
              output logic[WIDTH-1:0] y);
  
   always_comb
        case(s)
            2'b00: y = d0;
            2'b01: y = d1;
            3'b10: y = d2;
            default: y = d3;
        endcase
endmodule

`timescale 1ns / 1ps

# Test Bench
module test_bench();
    logic clk, reset;
    logic [31:0] pc, instr, writedata, dataadr, readdata;
    logic memwrite;
    top topMod(clk, reset, writedata, dataadr, pc, instr, readdata, memwrite);
    initial begin
        clk = 0;
        reset = 1; #100;
        reset = 0;
    end
    always #10 clk <= ~clk;
endmodule
